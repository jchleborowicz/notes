buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.6'
    }

}

apply plugin: 'org.asciidoctor.convert'

description = 'Learning notes'
version = '0.0.1'

//loading custom properties
if(new File('custom.gradle.properties').exists()){
    apply from: 'custom.gradle.properties'
}

build.dependsOn asciidoctor

task upload() {
    group 'Html operations'
    description 'Uploads html pages to s3.'

    dependsOn asciidoctor

    inputs.dir "${buildDir}/asciidoc/html5"
    outputs.upToDateWhen { true }

    doLast {
        exec {
            commandLine 'aws', '--profile', awsProfile, 's3', 'sync', '--delete', 'build/asciidoc/html5', awsTarget
        }
    }
}

def browse(String uriString) {
    java.awt.Desktop.desktop.browse uriString.toURI()
}

task openLocal() {
    group 'Html operations'
    description 'Opens local generated html index page.'

    dependsOn asciidoctor

    doLast {
        browse("file://$buildDir/asciidoc/html5/index.html")
    }
}

task openRemote() {
    group 'Html operations'
    description 'Opens remote (uploaded) html index page.'

    doLast {
        browse(notesUrl)
    }
}
